/*
 * Copyright 2024 by Patryk Goworowski and Patrick Michalik.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'

afterEvaluate {

    // Can pass version name to publish via `./gradlew -PversionNameArg=<version> publish`
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId 'com.wahoofitness.android'
                artifactId 'vico'
                version project.properties["versionNameArg"] ?: project.version

                pom {
                    name = 'Vico Graph Library'
                    description = 'Vico graph library'
                    url = 'http://github.com/WahooFitness/vico'
                }

                // Adjust .pom file with transitive dependencies
                pom.withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.findAll() {
                        boolean isGroupIsOk = it.groupId.text() != null && it.groupId.text() != 'com.wahoofitness.android'
                        boolean isArtifactIdOk = it.artifactId.text() != null && it.groupId.text() != 'unspecified'
                        boolean isVersionOk = it.version.text() != null && it.version.text() != 'unspecified'
                        !isGroupIsOk || !isArtifactIdOk || !isVersionOk
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/WahooFitness/vico")
                credentials {
                    username = System.getenv("GITHUB_USER")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
